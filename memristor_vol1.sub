*HP memristor - window approach
*Reliable Modeling of Ideal Generic Memristors
*via State-Space Transformation
*Appendix A
*nadogradnja koda da odgovara spice  modelu volatile memR Prodromakis iz 2009
* Copyright 2021 FTS UNS MEMR Research Group

.param Ron=1 Roff=100k uv=100e-12 D=10n qp=100e-9 qn=-80e-9  Rint=50k
+ k={uv*Ron/D**2} deltaR={Roff-Ron} p=10
.param x0=0.5 y0=0.5

*our window p=N
.func  f_ours(x)={(1-(2*x-1)**2)/(1-(2*x-1)**2+(2*x-1)**(2*p))}
.func iy(y,v,z)={if(v>0,if(z>qp,I(Emem)*uv*Ron*f_ours(y)/D**2,0),if(z<qn,I(Emem)*uv*Ron*f_ours(y)/D**2,0))}


.subckt memristor_vol1 1 2 x y z

*output cell
Roff 1 aux {Roff}
Emem aux 2 value={-deltaR*v(x)*I(Emem)}

*Charge cell
Gch 0 z value={I(Emem)}
Cz z 0 1 IC={0}
Rz z 0 0.1

*volatile cell
Gx 0 x value={I(Emem)*uv*Ron*f_ours(v(x))/D**2}
Cx x 0 5 IC={x0}
Rx x 3 1
Enov 3 0 value={v(y)}

*non volatile cell
Gy 0 y value={iy(v(y),v(1)-v(2),v(z))}
Cy y 0 1 IC={y0}

.ends memristor_vol1